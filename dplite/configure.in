dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during Tk installation
dnl	to configure the system for the local environment.
AC_INIT(dp.h)
AC_PROG_INSTALL
AC_PROG_RANLIB
CC=${CC-cc}
AC_SUBST(CC)

#--------------------------------------------------------------------
# Look for include files we need.
#--------------------------------------------------------------------
AC_HAVE_HEADERS(stdlib.h)
AC_HEADER_CHECK(sys/un.h, AC_DEFINE(UNIX_SOCKET), )
AC_HEADER_CHECK(errno.h, AC_DEFINE(HAVE_ERRNO_H), )
AC_HEADER_CHECK(sys/time.h, AC_DEFINE(HAVE_SYS_TIME_H), )
AC_HEADER_CHECK(malloc.h, AC_DEFINE(HAVE_MALLOC_H), )
AC_HEADER_EGREP(tolower, ctype.h, AC_DEFINE(HAVE_TOLOWER), )
AC_HEADER_EGREP(ntohl, netinet/in.h, AC_DEFINE(HAVE_NTOHL), )

#--------------------------------------------------------------------
#       Include sys/select.h if it exists and if it supplies things
#       that appear to be useful and aren't already in sys/types.h.
#       This appears to be true only on the RS/6000 under AIX.  Some
#       systems like OSF/1 have a sys/select.h that's of no use, and
#       other systems like SCO UNIX have a sys/select.h that's
#       pernicious.  If "fd_set" isn't defined anywhere then set a
#       special flag.
#--------------------------------------------------------------------
echo checking for sys/select.h
AC_COMPILE_CHECK(fd_set, [#include <sys/types.h>],
	[fd_set readMask, writeMask;], ,
	AC_HEADER_EGREP(fd_mask, sys/select.h, AC_DEFINE(HAVE_SYS_SELECT_H),
		AC_DEFINE(NO_FD_SET)))

#--------------------------------------------------------------------
# See if we have a writev() system call.  If not, define NO_WRITEV
#--------------------------------------------------------------------
AC_COMPILE_CHECK(writev, [#include <sys/types.h>
#include <sys/uio.h>],
	[struct iovec iov[2];  writev(1,iov,2);], ,
	AC_DEFINE(NO_WRITEV))

#--------------------------------------------------------------------
# Check for string.h and that it includes the right things (memset,
# memcpy, strncasecmp, and strerror).  If not, include our own.
#--------------------------------------------------------------------
AC_HEADER_CHECK(string.h, dp_ok=1, dp_ok=0)
AC_HEADER_EGREP(memset, string.h, , dp_ok=0)
AC_HEADER_EGREP(memcpy, string.h, , dp_ok=0)
AC_HEADER_EGREP(strerror, string.h, , dp_ok=0)
AC_HEADER_EGREP(strncasecmp, string.h, , dp_ok=0)
if test $dp_ok = 1; then
    AC_DEFINE(HAVE_STRING_H)
fi

#--------------------------------------------------------------------
#	Check for the existence of various libraries.  The order here
#	is important, so that then end up in the right order in the
#	command line generated by Make.
#--------------------------------------------------------------------

AC_HAVE_LIBRARY(socket, [LIBS="$LIBS -lsocket"])
AC_HAVE_LIBRARY(nsl, [LIBS="$LIBS -lnsl"])
AC_HAVE_LIBRARY(dnet_stub, [LIBS="$LIBS -ldnet_stub"])

AC_OUTPUT(Makefile)
