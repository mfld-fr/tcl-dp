# tcp.test --
#
#	Test the TCP protocol
#

# CMake creates testconfig.tcl from testconfig.tcl.in, substituting CMake variables.
source [file join tests testconfig.tcl]


test tcp-1.1 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -bar
    } msg] $msg
} -result {1 {unknown option "-bar", must be -async, -host, -myaddr, -myport -port or -server}}

test tcp-1.2 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -bar foo
    } msg] $msg
} -result {1 {unknown option "-bar", must be -async, -host, -myaddr, -myport -port or -server}}

test tcp-1.3 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -port
    } msg] $msg
} -result {1 {value for "-port" missing}}

test tcp-1.4 {dp_connect command} -body {
    list [catch {
	dp_connect tcp
    } msg] $msg
} -result {1 {option -port must be specified for clients}}

test tcp-1.5 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -port badport
    } msg] $msg
} -result {1 {expected integer but got "badport"}}

test tcp-1.6 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -server 1
    } msg] $msg
} -result {1 {option -myport must be specified for servers}}

test tcp-1.7 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -server 1 -myport 1234 -async 1
    } msg] $msg
} -result {1 {option -async is not valid for servers}}

test tcp-1.8 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -server 1 -myport 1234 -host localhost
    } msg] $msg
} -result {1 {option -host is not valid for servers}}

test tcp-1.9 {dp_connect command} -body {
    list [catch {
	dp_connect tcp -server 1 -myport 1234 -port 1234
    } msg] $msg
} -result {1 {option -port is not valid for servers}}

test tcp-2.0.1 {Opening port with no service.} -body {
    list [catch {
        set csock [dp_connect tcp -host localhost -port 14466]
    } msg] $msg
} -result {1 {couldn't open socket: connection refused}}

test tcp-2.1 {dp_accept command} -body {
    list [catch {dp_accept} message] $message
} -result {1 {wrong # args: should be "dp_accept channelId"}}

test tcp-2.2 {dp_accept command} -body {
    list [catch {
	set ssock [dp_connect tcp -server 1 -myport 14467]
	set csock [dp_connect tcp -host localhost -port 14467]
	after 500
	set asock [lindex [dp_accept $ssock] 0]
	puts $csock hello1
	set line [gets $asock]
    } msg] $msg
} -result {0 hello1}

catch {close $ssock}
catch {close $csock}
catch {close $asock}

test tcp-2.3 {Closing of server sockets} -body {
    list [catch {
        set csock [dp_connect tcp -host localhost -port 14470]
    } msg] $msg
} -result {1 {couldn't open socket: connection refused}}

catch {close $csock}

test tcp-2.4 {Closing of client sockets} -body {
    set ssock [dp_connect tcp -server 1 -myport 14468]
    set csock [dp_connect tcp -host localhost -port 14468 -async 1]
    after 500
    set asock [lindex [dp_accept $ssock] 0]
    close $asock
    after 1000
    catch {
	puts $csock hello1
    } msg
    regexp {^error writing} $msg
} -result [regexp {^error writing} {error writing "tcp2": broken pipe}]

catch {close $csock}
catch {close $ssock}

test tcp-2.5 {asynchronous client Tcp socket} -body {
    list [catch {
	set ssock [dp_connect tcp -server 1 -myport 14469]
	set csock [dp_connect tcp -host localhost -port 14469 -myport 14470 -async 1]
	after 500
	set asock [lindex [dp_accept $ssock] 0]
	puts $csock hello1
	set line [gets $asock]
    } msg] $msg
} -result {0 hello1}

#
# fconfigure tests
#

catch {set myIpAddr [dp_netinfo -address [info hostname]]}
if {$myIpAddr == 0} {
    puts stdout "Error determining IP address of this machine"
}

# With tcp buffer size autotuning in newer OSs, the buffer
# sizes that "fconfigure" returns for the send and receive
# buffers are not necessarily the default value set by dp.
# So get the size with "fconfigure $ssock -sendBuffer/recvBuffer".
set sendBufferSize [fconfigure $ssock -sendBuffer]
set recvBufferSize [fconfigure $ssock -recvBuffer]

test tcp-2.6.13.1 {fconfigure tcp (ssock)} -body {
	fconfigure $ssock
} -returnCodes 0 -result [list -blocking 1 -buffering none -buffersize 4096 -encoding binary -eofchar {{} {}} -translation {lf lf} -keepAlive 0 -linger 0 -recvBuffer $recvBufferSize -reuseAddr 1 -sendBuffer $sendBufferSize -myIpAddr $myIpAddr -myport 14469 -destIpAddr 0.0.0.0 -destport 0]

set sendBufferSize [fconfigure $csock -sendBuffer]
set recvBufferSize [fconfigure $csock -recvBuffer]
test tcp-2.6.13.2 "fconfigure tcp (csock)" -body {
	fconfigure $csock
} -returnCodes 0 -result [list -blocking 1 -buffering none -buffersize 4096 -encoding binary -eofchar {{} {}} -translation {lf lf} -keepAlive 0 -linger 0 -recvBuffer $recvBufferSize -reuseAddr 1 -sendBuffer $sendBufferSize -myIpAddr $myIpAddr -myport 14470 -destIpAddr 127.0.0.1 -destport 14469]

set sendBufferSize [fconfigure $asock -sendBuffer]
set recvBufferSize [fconfigure $asock -recvBuffer]
test tcp-2.6.13.3 "fconfigure tcp (asock)" -body {
	fconfigure $asock
} -returnCodes 0 -result [list -blocking 1 -buffering none -buffersize 4096 -encoding binary -eofchar {{} {}} -translation {lf lf} -keepAlive 0 -linger 0 -recvBuffer $recvBufferSize -reuseAddr 1 -sendBuffer $sendBufferSize -myIpAddr $myIpAddr -myport 14469 -destIpAddr 127.0.0.1 -destport 14470]



catch {set myIpAddr [dp_netinfo -address [info hostname]]}
if {$myIpAddr == 0} {
    puts stdout "Error determining IP address of this machine"
}

test tcp-2.6 {fconfigure tcp} -body {

    foreach type "ssock csock asock" {
	set sock [set $type]

	test tcp-2.6.2 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -blocking 0
	    } msg] $msg
	} -result {0 {}} 

	test tcp-2.6.3 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -blocking
	    } msg] $msg
	} -result {0 0}

	test tcp-2.6.4 "fconfigure tcp ($type)" -body {
		fconfigure $sock -sendBuffer 128
	} -returnCodes 0 -result {}

	# With tcp buffer size autotuning in newer OSs, the buffer
	# size that "fconfigure -sendBuffer" returns is not
	# necessarily the value we set it to above.  So just
	# test for any nonzero number.
	test tcp-2.6.5 "fconfigure tcp ($type)" -body {
		fconfigure $sock -sendBuffer
	} -returnCodes 0 -match regexp -result {[1-9][0-9]*}

	test tcp-2.6.6 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -sendBuffer -1
	    } msg] $msg
	} -result {1 {Buffer size must be > 0}}

	test tcp-2.6.7 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -sendBuffer foo
	    } msg] $msg
	} -result {1 {expected integer but got "foo"}}

	test tcp-2.6.8 "fconfigure tcp ($type)" -body {
		fconfigure $sock -recvBuffer 256
	} -returnCodes 0 -result {}

	# With tcp buffer size autotuning in newer OSs, the buffer
	# size that "fconfigure -recvBuffer" returns is not
	# necessarily the value we set it to above.  So just
	# test for any nonzero number.
	test tcp-2.6.9 "fconfigure tcp ($type)" -body {
		fconfigure $sock -recvBuffer
	} -returnCodes 0 -match regexp -result {[1-9][0-9]*}

	test tcp-2.6.10 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -recvBuffer -1
	    } msg] $msg
	} -result {1 {Buffer size must be > 0}}

	test tcp-2.6.11 "fconfigure tcp ($type)" -body {
	    list [catch {
		fconfigure $sock -recvBuffer foo
	    } msg] $msg
	} -result {1 {expected integer but got "foo"}}
    }

	foreach opt {-keepAlive -reuseAddr} {
	    test tcp-2.6.12.0 "fconfigure tcp ($type)" -body {
		list [catch {
		    fconfigure $sock $opt foo
		} msg] $msg
	    } -result {1 {expected boolean value but got "foo"}}

	    test tcp-2.6.12.1 "fconfigure tcp ($type)" -body {
		list [catch {
		    fconfigure $sock $opt 0
		} msg] $msg
	    } -result {0 {}}

	    test tcp-2.6.12.2 "fconfigure tcp ($type)" -body {
		list [catch {
		    fconfigure $sock $opt
		} msg] $msg
	    } -result {0 0}

	    test tcp-2.6.12.3 "fconfigure tcp ($type)" -body {
		list [catch {
		    fconfigure $sock $opt 1
		} msg] $msg
	    } -result {0 {}}

	    test tcp-2.6.12.4 "fconfigure tcp ($type)" -body {
		list [catch {
		    fconfigure $sock $opt
		} msg] $msg
	    } -result {0 1}
	}

    set result_is ok
} -result ok

catch {close $ssock}
catch {close $csock}
catch {close $asock}

# CORNELL ONLY TESTS

# (ToDo) Connect to a "test server" instead.
#
test cornell-tcp-3.1 {dp_connect command} -body {
    set chan [dp_connect tcp -host www -port 80]

    puts $chan "GET /"
    flush $chan
    string range [gets $chan] 0 5
} -result {<!DOCT}

::tcltest::cleanupTests
return $::tcltest::numTests(Failed)

